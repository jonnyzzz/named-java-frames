buildscript {
  ext.kotlin_version = '1.1.0'

  repositories { mavenCentral() }
  dependencies { classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version" }
}

plugins {
  id "com.jfrog.bintray" version "1.5"
}

version = project.hasProperty('teamcity') ? project['teamcity']['build.number'] : 'SNAPSHOT'

subprojects {
  apply plugin: 'java'

  group = 'org.jonnyzzz.named-frames'

  sourceCompatibility = 1.6
  targetCompatibility = 1.6

  repositories {
    mavenCentral()
  }

  dependencies {

    testCompile "junit:junit:4.12"
  }

  test {
    useJUnit()
  }

  apply plugin: 'maven'
  apply plugin: 'maven-publish'
  apply plugin: "com.jfrog.bintray"

  task sourceJar(type: Jar) {
    from sourceSets.main.allSource
  }

  artifacts {
    archives sourceJar
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
        artifact sourceJar {
          classifier "sources"
        }
      }
    }
  }

  if (hasProperty('bintrayUser')) {
    bintray {
      user = getProperty('bintrayUser')
      key = getProperty('bintrayApiKey')
      publications = ['mavenJava']
      pkg {
        repo = 'maven'
        name = 'named-frames'
        userOrg = getProperty('bintrayUser')
        licenses = ['MIT']
        vcsUrl = 'https://github.com/jonnyzzz/named-frames'
      }
    }
  }
}

configure(project(':named-frames')) {
  dependencies {
    compile 'org.jetbrains:annotations:13.0'
  }
}

configure(project(':named-frames-kotlin')) {
  apply plugin: 'kotlin'

  compileKotlin {
    kotlinOptions {
      jvmTarget = "1.6"
    }
  }

  compileTestKotlin {
    kotlinOptions {
      jvmTarget = "1.6"
    }
  }
  
  dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile project(':named-frames')
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '3.4.1'
}

task bintrayUploadEx {
  dependsOn project(':named-frames').tasks.bintrayUpload
  dependsOn project(':named-frames-kotlin').tasks.bintrayUpload
}

task localUploadEx {
  dependsOn { ':named-frames:publishToLocalMaven' }
  dependsOn { ':named-frames-kotlin:publishToLocalMaven' }
}
